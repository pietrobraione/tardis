plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

def javaHome = javaToolchains.compilerFor { languageVersion = javaVersion }.get().metadata.installationPath.asFile.toString()

repositories {
    flatDir {
    	dirs "${javaHome}/lib", "${project(':jbse').projectDir.toString()}/libs"
    }
}

dependencies {
    implementation 'args4j:args4j:2.32'
    implementation 'com.github.javaparser:javaparser-core:3.15.9'
    implementation name: 'javassist'
    implementation name: 'tools'
    implementation project(':sushi-lib')
    implementation project(':jbse')
}

shadowJar {
    classifier = 'shaded'
    exclude 'tools.jar'
    relocate 'javassist', 'tardis.javassist'
    relocate 'jbse', 'tardis.jbse'
    relocate 'com.github.javaparser', 'tardis.com.github.javaparser'
    relocate 'org.jacoco', 'tardis.org.jacoco'
    relocate 'org.objectweb', 'tardis.org.objectweb'
    relocate 'org.kohsuke', 'tardis.org.kohsuke'
    relocate 'sushi', 'tardis'
}

build.dependsOn 'shadowJar'

application {
	mainClassName='tardis.Main' //deprecated, but still used by the shadow plugin
	mainClass.set('tardis.Main')
}

jar {
    manifest {
        attributes 'Implementation-Title': 'TARDIS',
                   'Implementation-Vendor': 'University of Milano-Bicocca',
                   'Implementation-Version': archiveVersion
    }
}