plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

def javaHome = javaToolchains.compilerFor { languageVersion = javaVersion }.get().metadata.installationPath.asFile.toString()

repositories {
    flatDir {
    	dirs "${javaHome}/lib", "${project(':jbse').projectDir.toString()}/libs", "${project.rootDir.toString()}/libs" //for tools.jar and for the patched javassist.jar
    }
}

dependencies {
    implementation 'args4j:args4j:2.32'
    implementation 'com.github.javaparser:javaparser-core:3.15.9'
    implementation 'org.apache.logging.log4j:log4j-api:2.14.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.0'
    implementation name: 'tools'
    implementation name: 'evosuite-shaded-1.2.1-SNAPSHOT'
    implementation project(':sushi-lib')
    implementation project(':jbse')
}

task wrapper(type: Wrapper) {
    gradleVersion = '7.1.1'
}

task copyToLib(type: Copy) {
    into "deps"
    from configurations.compileClasspath
}

shadowJar {
    classifier = 'shaded'
    exclude 'tools.jar'
    relocate 'com.github.javaparser', 'tardis.com.github.javaparser'
    relocate 'javassist', 'tardis.javassist'
    relocate 'org.apache.logging.log4j', 'tardis.org.apache.logging.log4j'
    relocate 'org.jacoco', 'tardis.org.jacoco'
    relocate 'org.kohsuke', 'tardis.org.kohsuke'
    relocate 'org.objectweb', 'tardis.org.objectweb'
}

build.dependsOn(copyToLib)

build.dependsOn 'shadowJar'

application {
	mainClass.set('tardis.Main')
}

jar {
    manifest {
        attributes 'Implementation-Title': 'TARDIS',
                   'Implementation-Vendor': 'University of Milano-Bicocca',
                   'Implementation-Version': archiveVersion
    }
}